plugins {
	id "com.android.library" version "8.8.0" apply false
    id 'de.undercouch.download' version '5.6.0' apply false
}

project(':sqlite-android') {
    // Force :requery-root to be ready first
    evaluationDependsOn(':requery-root')

    if (!rootProject.hasProperty("VERSION_NAME") && project(':requery-root').hasProperty("VERSION_NAME")) {
        project.ext.set("VERSION_NAME", project(':requery-root').getProperty("VERSION_NAME"))
    }

    if (project.hasProperty("POM_ARTIFACT_ID")) {
        project.ext.set("POM_ARTIFACT_ID", rootProject.getProperty("POM_ARTIFACT_ID"))
    }
    if (project.hasProperty("POM_NAME")) {
        project.ext.set("POM_NAME", rootProject.getProperty("POM_NAME"))
    }
    if (project.hasProperty("POM_DESCRIPTION")) {
        project.ext.set("POM_DESCRIPTION", rootProject.getProperty("POM_DESCRIPTION"))
    }

    // Only apply to modules that use the Android Library plugin
    plugins.withId('com.android.library') {
        android {
            libraryVariants.all { variant ->
                variant.outputs.all { output ->
                    if (outputFileName.endsWith('.aar')) {
                        // Custom naming logic: [ProjectName]-[Variant]-[Version].aar
                        //outputFileName = "${project.name}-${variant.name}-${project.version}.aar"
                        outputFileName = "${rootProject.name}-${variant.name}.aar"
                    }
                }
            }
        }
        archivesBaseName = "${rootProject.name}-${project.name}"

        tasks.named('preBuild') {
            dependsOn ':configureBuild'
            dependsOn ':installSqliteHeader'
        }
    }
}

ext {
    decimalDistributionUrl = "https://raw.githubusercontent.com/sqlite/sqlite/master/ext/misc/decimal.c"
    sqliteNativeSources = "sqlite-android/sqlite-android/src/main/jni/sqlite"
}

apply plugin: "de.undercouch.download"

// Apply patches before upstream compilation
tasks.register("configureBuild") {
    doLast {
        // Download decimals directly from SQLite mirror in the github
        project.download.run {
            src decimalDistributionUrl
            //dest layout.buildDirectory.file("decimal.c").get().asFile
            dest sqliteNativeSources
            overwrite false
        }

        // Copy files from files
        copy {
            from "${rootDir}/src"
            into sqliteNativeSources
        }

        // Patch files
        file("${rootDir}/patches").listFiles()?.each { patch ->
            if (patch.isFile()) {
                def execResult = exec {
                    commandLine 'git', 'apply', '--ignore-space-change', '--check', '--reverse', patch.absolutePath
                    ignoreExitValue = true
                }
                if (execResult.exitValue != 0) {
                    exec {
                        commandLine 'git', 'apply', '--ignore-space-change', patch.absolutePath
                    }
                    logger.lifecycle("Patch '${patch.name}' has been applied.")
                } else {
                    logger.lifecycle("Patch '${patch.name}' already applied.")
                }
            }
        }
    }
}

tasks.register("installSqliteHeader") {
    def downloadTask = project(":sqlite-android").tasks.named("downloadSqlite")
    inputs.file(downloadTask.map { it.dest })

    doLast {
        copy {
            // Use zipTree on the 'dest' file from the download task
            from zipTree(downloadTask.get().dest)
            into "sqlite-android/sqlite-android/src/main/jni/sqlite"
            // Filter for the specific file
            include "**/sqlite3ext.h"
            // Flatten the structure so it's not nested in zip folders
            eachFile { it.path = it.name }
        }
    }
}

task regeneratePatch {
    group = "maintenance"
    description = "Generate patches"

    doLast {
        file("${rootDir}/build").listFiles()?.each { target ->
            if (!target.isDirectory()) {
                println "Generating patch for " + target.name
                def patchFile = file("${rootDir}/patches/${target.name}.patch")
                exec {
                    // Command: git diff --patch --unified=5 --no-index --output=patch_file source_file target_file
                    commandLine 'git', 'diff', '--patch', '--unified=5', '--no-index', "--output=${patchFile.absolutePath}", sqliteNativeSources + "/" + target.name, target.absolutePath
                    ignoreExitValue = true
                }
                if (patchFile.exists()) {
                    if (patchFile.length() == 0) {
                        patchFile.delete()
                    } else {
                        ant.replaceregexp(
                            file: "${rootDir}/patches/${target.name}.patch",
                            match: "b${target.absolutePath}",
                            replace: "b/${sqliteNativeSources + "/" + target.name}",
                            flags: 'g'
                        )
                        println "Patch generated for " + target.name
                    }
                }
            }
        }
    }
}